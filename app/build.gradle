apply plugin: 'com.android.application'
apply from: rootProject.file('script.gradle')

/**
 * 应用名称
 */
def appName = 'FULiveDemo'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.faceunity.${appName}".toLowerCase()
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.namaSdkVersionCode
        versionName rootProject.ext.namaSdkVersionName
        manifestPlaceholders = ['appName': appName]
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // output: FULiveDemo_6.1.0-release3_debug_20190531_101331.apk
    applicationVariants.all { variant ->
        variant.outputs.all {
            def releaseTime = new Date().format("yyyyMMdd_HHmmss", TimeZone.getDefault())
            outputFileName = "${appName}_${versionName}_${variant.buildType.name}_${releaseTime}.apk"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {
    implementation project(':faceunity')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportCompatV7Version}"
    implementation "com.android.support:cardview-v7:${rootProject.ext.supportCompatV7Version}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportCompatV7Version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.bumptech.glide:glide:4.9.0' // do not upgrade
    implementation 'com.wuyr:pathlayoutmanager:1.0.3'
    implementation 'com.kyleduo.switchbutton:library:2.0.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
}
