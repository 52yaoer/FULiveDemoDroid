apply plugin: 'com.android.application'

/**
 * 应用名称
 */
def appName = 'FULiveDemo'
/**
 *  SDK 版本号
 */
def sdkVersionName = '6.2.0-release1'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.faceunity.${appName}".toLowerCase()
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.appVersionCode
        versionName sdkVersionName
        manifestPlaceholders = ['appName': appName]
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // output: FULiveDemo_6.1.0-release3_debug_20190531_101331.apk
    applicationVariants.all { variant ->
        variant.outputs.all {
            def releaseTime = new Date().format("yyyyMMdd_HHmmss", TimeZone.getDefault())
            outputFileName = "${appName}_${versionName}_${variant.buildType.name}_${releaseTime}.apk"
        }
    }

    sourceSets {
        main.assets.srcDirs += ['src/main/assets', 'src/main/effects/bundle', 'src/main/makeup/material',
                                "src/main/poster", "src/main/livephoto"]
        main.res.srcDirs += ['src/main/res', 'src/main/effects/res', 'src/main/makeup/res']
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':faceunity')
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.supportCompatV7Version}"
    implementation "com.android.support:cardview-v7:${rootProject.ext.supportCompatV7Version}"
    implementation "com.android.support:recyclerview-v7:${rootProject.ext.supportCompatV7Version}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.wuyr:pathlayoutmanager:1.0.3'
}
